plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'com.google.devtools.ksp'
    id 'maven-publish'
}

dependencies {
    debugImplementation "io.objectbox:objectbox-android-objectbrowser:$objectboxVersion"
    releaseImplementation "io.objectbox:objectbox-android:$objectboxVersion"
}

apply plugin: 'io.objectbox'

android {

    namespace 'net.ischool.isus'

    compileSdkVersion COMPILE_SDK_VERSION
    defaultConfig {
        minSdkVersion MIN_SDK_VERSION
        targetSdkVersion COMPILE_SDK_VERSION
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexKeepFile file('multidex-config.txt')
        buildConfigField "String", "ISUS_LIB_VERSION", "\"$ISUS_VERSION\""
        buildConfigField "int", "ISUS_LIB_VERSION_CODE", "$ISUS_VERSION_CODE"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17.toString()
    }


    buildFeatures {
        viewBinding true
    }

    configurations {
        all*.exclude group: 'com.google.code.gson'
    }

    publishing {
        singleVariant('release') {
            withSourcesJar()
            withJavadocJar()
        }
    }
    lint {
        abortOnError false
    }
}

dependencies {
    api fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.9.0'
    implementation 'androidx.legacy:legacy-support-v13:1.0.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.7.1"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.1"
    // RxKotlin
    implementation "io.reactivex.rxjava3:rxkotlin:3.0.1"
    // RxAndroid
    implementation 'io.reactivex.rxjava3:rxandroid:3.0.2'
    // RxLifecycle
    implementation 'com.trello.rxlifecycle4:rxlifecycle-kotlin:4.0.2'
    implementation 'com.trello.rxlifecycle4:rxlifecycle-components:4.0.2'
    // RxBinding
    implementation 'com.jakewharton.rxbinding4:rxbinding:4.0.0'
    implementation 'com.jakewharton.rxbinding4:rxbinding-core:4.0.0'
    // RxPreference
    implementation 'com.f2prateek.rx.preferences2:rx-preferences:2.0.1'
    // OKHttp3
    implementation 'com.squareup.okhttp3:okhttp:4.11.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.11.0'
    // Server-Sent Events
    implementation 'com.launchdarkly:okhttp-eventsource:4.1.1'
    // Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava3:2.9.0'
    // Gson
    implementation 'com.google.code.gson:gson:2.10.1'
    // RxPermission
    implementation 'com.github.tbruyelle:rxpermissions:0.12'
    // Logger
    implementation 'com.orhanobut:logger:2.2.0'
    // Useful Extension
    implementation 'com.github.Walker-ZHAO:Anke:0.3.2'
    // RabbitMQ
    implementation 'com.rabbitmq:amqp-client:4.12.0'
    // ObjectBox
    implementation "io.objectbox:objectbox-kotlin:$objectboxVersion"
    // ZXing
    implementation 'com.google.zxing:core:3.5.3'
    // CameraX
    def camerax_version = "1.2.3"
    implementation "androidx.camera:camera-core:${camerax_version}"
    implementation "androidx.camera:camera-camera2:${camerax_version}"
    implementation "androidx.camera:camera-lifecycle:${camerax_version}"
    implementation "androidx.camera:camera-view:${camerax_version}"
    implementation "androidx.camera:camera-extensions:${camerax_version}"
    // Activity Result API
    implementation 'androidx.activity:activity-ktx:1.7.2'
    implementation 'androidx.fragment:fragment-ktx:1.5.7'
    // 希沃SDK
    compileOnly project(':seewo')
}

afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                // Applies the component for the release build variant.
                from components.release

                // You can then customize attributes of the publication as shown below.
                groupId = 'com.github.Walker-ZHAO'
                artifactId = 'ISUS'
                version = ISUS_VERSION
            }
        }
    }
}
